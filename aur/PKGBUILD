# Maintainer: Burgess <burgess@example.com>
pkgname=krusty
pkgver=0.3.0
pkgrel=1
pkgdesc="The fun coding assistant"
arch=('x86_64' 'aarch64')
url="https://github.com/BurgessTG/Krusty"
license=('MIT')
depends=('gcc-libs' 'openssl')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/BurgessTG/Krusty/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

prepare() {
    cd "Krusty-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "Krusty-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    export CARGO_PROFILE_RELEASE_LTO=false
    cargo build --frozen --release
}

check() {
    cd "Krusty-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo test --frozen --release
}

package() {
    cd "Krusty-$pkgver"
    install -Dm755 "target/release/krusty" "$pkgdir/usr/bin/krusty"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
